; Script generated by the Inno Setup Script Wizard and ChatGPT, with manual revisions.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Triggerless Triggerbot"
#define MyAppVersion "1.0.5.2512"
#define MyAppPublisher "triggerless.com"
#define MyAppURL "https://www.triggerless.com/triggerbot/"
#define MyAppExeName "TriggerBot.exe"
#define MyAppIcoName "note3d.ico"
#define ProjectPath "D:\DEV\CS\triggerless-triggerbot\Triggerless.TriggerBot"
#define ReleaseBin "D:\DEV\CS\triggerless-triggerbot\Triggerless.TriggerBot\bin\x64\Release"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{B698BC6A-2407-4B85-B87B-6F638BF423E7}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={userappdata}\Triggerless\Triggerbot
DisableDirPage=yes
DisableProgramGroupPage=yes
; Remove the following line to run in administrative install mode (install for all users.)
PrivilegesRequired=lowest
OutputBaseFilename=mysetup
Compression=lzma
SolidCompression=yes
WizardStyle=modern
SetupIconFile=D:\DEV\CS\triggerless-triggerbot\Triggerless.TriggerBot\assets\note10.ico


[Code]

// Send installation results to the server.
const
  ApiUrl = 'https://www.triggerless.com/api/bot/sendmessage';
  AppVer = '{#SetupSetting("AppVersion")}';   // compile-time inject of the version
  // Where IMVU keeps logs: %APPDATA%\IMVU
  ImvuSubDir = 'IMVU';
  FirstLog   = 'IMVULog.log';
  // Used for file read
  GENERIC_READ       = $80000000;
  FILE_SHARE_READ    = $00000001;
  FILE_SHARE_WRITE   = $00000002;
  FILE_SHARE_DELETE  = $00000004;
  OPEN_EXISTING         = 3;
  INVALID_HANDLE_VALUE  = -1;
  MAX_READ = 2147483647;

var
  InstallSucceeded: Boolean;
  CID: Int64;

// ---------------------------------
// This is used to determine when and how to install the Liberation Sans font
// as a user-specfic system font.

function PerUserFontDir: string;
begin
  Result := ExpandConstant('{localappdata}\Microsoft\Windows\Fonts');
end;

function ShouldInstallFont(const FileName, FaceName: string): Boolean;
begin
  Result :=
    (not FontExists(FaceName)) and
    (not FileExists(PerUserFontDir + '\' + FileName));
end;

function GetAppData: string;
begin
  Result := ExpandConstant('{userappdata}');
end;

// This gets where IMVU stores the Log files.

function ImvuFileLocation: string;
begin
  Result := AddBackslash(GetAppData) + ImvuSubDir;
end;

// =======================================================================================
// This is the boiler plate Pascal you get with every new ISS project.

function IsAppInstalled(): Boolean;
var
  UninstallKey: string;
begin
  // Specify the uninstallation registry key for your application
  UninstallKey := 'Software\Microsoft\Windows\CurrentVersion\Uninstall\{B698BC6A-2407-4B85-B87B-6F638BF423E7}';

  // Check if the uninstallation registry key exists
  Result := RegKeyExists(HKCU, UninstallKey);
end;

function GetUninstallerPath(): string;
var
  UninstallKey: string;
begin
  // Specify the uninstallation registry key for your application
  UninstallKey := 'Software\Microsoft\Windows\CurrentVersion\Uninstall\{B698BC6A-2407-4B85-B87B-6F638BF423E7}';

  // Read the UninstallString value from the uninstallation registry key
  Result := '';

  if RegQueryStringValue(HKCU, UninstallKey, 'UninstallString', Result) then
  begin
    // Remove any quotes surrounding the uninstaller path
    Result := RemoveQuotes(Result);
  end;
end;

function ReadCidFromUserConfig(const CompanyName, AppExeName, Version: string): Int64;
var
  AppData, Root, HashMask, HashDir, CandidateCfg: string;
  FR: TFindRec;
  FileLines: TArrayOfString;
  I, StartPos, EndPos: Integer;
  Line, ValueStr: string;
begin
  Result := 0;

  try
    AppData := GetEnv('LOCALAPPDATA');
    if AppData = '' then Exit;

    Root := AddBackslash(AddBackslash(AppData) + CompanyName);
    HashMask := Root + AppExeName + '_*';

    { Enumerate "...\<Company>\<AppExeName>_*" directories }
    if FindFirst(HashMask, FR) then
    begin
      try
        repeat
          if (FR.Attributes and FILE_ATTRIBUTE_DIRECTORY) <> 0 then
          begin
            { Build exact path: ...\hash\<Version>\user.config }
            HashDir := AddBackslash(Root + FR.Name);
            CandidateCfg := AddBackslash(HashDir + Version) + 'user.config';

            if FileExists(CandidateCfg) then
            begin
              { Load and parse Cid }
              if not LoadStringsFromFile(CandidateCfg, FileLines) then Exit;

              for I := 0 to GetArrayLength(FileLines) - 1 do
              begin
                Line := Trim(FileLines[I]);

                if Pos('name="Cid"', Line) > 0 then
                begin
                  { Common case: value on next line }
                  if I + 1 >= GetArrayLength(FileLines) then Exit;

                  Line := Trim(FileLines[I + 1]);
                  StartPos := Pos('<value>', Line);
                  EndPos := Pos('</value>', Line);

                  if (StartPos = 0) or (EndPos = 0) or (EndPos <= StartPos) then Exit;

                  ValueStr := Copy(
                    Line,
                    StartPos + Length('<value>'),
                    EndPos - (StartPos + Length('<value>'))
                  );

                  try
                    Result := StrToInt64(ValueStr);
                  except
                    Result := 0;
                  end;

                  Exit;
                end;
              end;

              { If user.config exists but Cid not found, treat as failure }
              Exit;
            end;
          end;
        until not FindNext(FR);
      finally
        FindClose(FR);
      end;
    end;

  except
    Result := 0;
  end;
end;


function InitializeSetup(): Boolean;
var
  UninstallExePath: string;
  ResultCode: Integer;
begin
  CID := ReadCidFromUserConfig('triggerless.com', 'TriggerBot.exe', '1.0.4.2510');
  Log('Initial CID read: ' + IntToStr(CID));
  // Check if the application is already installed
  if IsAppInstalled() then
  begin
    // Get the path to the uninstaller for the currently installed version
    UninstallExePath := GetUninstallerPath();

    // Execute the uninstaller if the path is valid
    if UninstallExePath <> '' then
    begin
      // Execute the uninstaller and wait for it to finish
      Exec('"' + UninstallExePath + '"', '/SILENT', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);

      // Check if the uninstallation was successful
      if ResultCode = 0 then
      begin
        // Uninstallation succeeded, continue with the installation
        Result := True;
      end
      else
      begin
        // Uninstallation failed, abort the installation
        MsgBox('Unable to uninstall the previous version. Please manually uninstall the application before proceeding.', mbError, MB_OK);
        Result := False;
      end;
    end
    else
    begin
      // Invalid uninstaller path, abort the installation
      MsgBox('Unable to find the uninstaller for the previous version. Please manually uninstall the application before proceeding.', mbError, MB_OK);
      Result := False;
    end;
  end
  else
  begin
    Result := True; // Continue with the installation
  end;
end;

// Added the following to track installations 2025-09-26

function PostJson(const Url, Json: string): Boolean;
var
  Http: Variant;
begin
  Result := False;
  try
    Http := CreateOleObject('WinHttp.WinHttpRequest.5.1');
    // synchronous POST
    Http.Open('POST', Url, False);
    Http.SetRequestHeader('Content-Type', 'application/json');
    // Optional: identify the installer (good for server logs)
    Http.SetRequestHeader('User-Agent', 'InnoSetup/Triggerbot');
	  // Customer tracking header

    Http.setRequestHeader('CustomerID', IntToStr(CID));
    Http.Send(Json);

    if (Http.Status >= 200) and (Http.Status < 300) then
    begin
      Log('POST ok: ' + IntToStr(Integer(Http.Status)) + ' ' + string(Http.StatusText));
      Result := True;
    end
    else
    begin
      Log(
          'POST failed: ' +
          IntToStr(Integer(Http.Status)) + ' ' +
          string(Http.StatusText) + ' ' +
          string(Http.ResponseText));
    end;
  except
    Log('POST exception: ' + GetExceptionMessage);
  end;
end;

procedure InitializeWizard;
begin
  InstallSucceeded := False;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  // If we reach ssDone, setup completed successfully
  if CurStep = ssDone then
    InstallSucceeded := True;
end;

procedure DeinitializeSetup;
var
  title, body, json: string;
begin
  // Fires when the wizard closes in all cases (success, cancel, error)
  title := 'Triggerbot Installation';
  if InstallSucceeded then
    body := Format('Version %s installation successful', [AppVer])
  else
    body := Format('Version %s installation failed', [AppVer]);

  // Minimal JSON building (no special chars in these strings)
  json := Format('{"title":"%s","body":"%s"}', [title, body]);

  // Best-effort POST; errors are logged but do not block the installer
  if not PostJson(ApiUrl, json) then
    Log('Notification POST did not succeed.');
end;



[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}";

[Files]
Source: "{#ProjectPath}\assets\note3d.ico"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ProjectPath}\assets\LiberationSans_Regular.ttf"; DestDir: "{localappdata}\Microsoft\Windows\Fonts"; FontInstall: "Liberation Sans"; Flags: onlyifdoesntexist uninsneveruninstall; Check: ShouldInstallFont('LiberationSans_Regular.ttf', 'Liberation Sans')
Source: "{#ProjectPath}\assets\LiberationSans_Bold.ttf"; DestDir: "{localappdata}\Microsoft\Windows\Fonts"; FontInstall: "Liberation Sans Bold"; Flags: onlyifdoesntexist uninsneveruninstall; Check: ShouldInstallFont('LiberationSans_Bold.ttf', 'Liberation Sans Bold')
Source: "{#ProjectPath}\assets\LiberationSans_Italic.ttf"; DestDir: "{localappdata}\Microsoft\Windows\Fonts"; FontInstall: "Liberation Sans Italic"; Flags: onlyifdoesntexist uninsneveruninstall; Check: ShouldInstallFont('LiberationSans_Italic.ttf', 'Liberation Sans Italic')
Source: "{#ProjectPath}\assets\LiberationSans_BoldItalic.ttf"; DestDir: "{localappdata}\Microsoft\Windows\Fonts"; FontInstall: "Liberation Sans Bold Italic"; Flags: onlyifdoesntexist uninsneveruninstall; Check: ShouldInstallFont('LiberationSans_BoldItalic.ttf', 'Liberation Sans Bold Italic')
Source: "{#ReleaseBin}\ffmpeg\ffmpeg.exe"; DestDir: "{app}\ffmpeg"; Flags: ignoreversion
Source: "{#ReleaseBin}\ffmpeg\ffprobe.exe"; DestDir: "{app}\ffmpeg"; Flags: ignoreversion

Source: "{#ReleaseBin}\x64\SQLite.Interop.dll"; DestDir: "{app}\x64"; Flags: ignoreversion
Source: "{#ReleaseBin}\x86\SQLite.Interop.dll"; DestDir: "{app}\x86"; Flags: ignoreversion
Source: "{#ReleaseBin}\BouncyCastle.Crypto.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\Dapper.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\EntityFramework.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\EntityFramework.SqlServer.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\ICSharpCode.SharpZipLib.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\ManagedWinapi.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\ManagedWinapiNativeHelper.dll"; DestDir: "{app}"; Flags: ignoreversion 
Source: "{#ReleaseBin}\Microsoft.Bcl.AsyncInterfaces.dll"; DestDir: "{app}"; Flags: ignoreversion 
Source: "{#ReleaseBin}\Microsoft.Extensions.DependencyInjection.Abstractions.dll"; DestDir: "{app}"; Flags: ignoreversion 
Source: "{#ReleaseBin}\Microsoft.Extensions.DependencyInjection.dll"; DestDir: "{app}"; Flags: ignoreversion 
Source: "{#ReleaseBin}\Microsoft.Extensions.Logging.Abstractions.dll"; DestDir: "{app}"; Flags: ignoreversion 
Source: "{#ReleaseBin}\Microsoft.Win32.Registry.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\Microsoft.WindowsAPICodePack.Core.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\Microsoft.WindowsAPICodePack.Shell.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\NAudio.Asio.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\NAudio.Core.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\NAudio.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\NAudio.Midi.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\NAudio.Vorbis.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\NAudio.Wasapi.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\NAudio.WaveFormRenderer.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\NAudio.WinForms.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\NAudio.WinMM.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\Newtonsoft.Json.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\NVorbis.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\System.Buffers.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\System.Collections.Immutable.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\System.Data.SQLite.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\System.Diagnostics.DiagnosticSource.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\System.Drawing.Common.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\System.Memory.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\System.Numerics.Vectors.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\System.Runtime.CompilerServices.Unsafe.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\System.Security.AccessControl.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\System.Security.Principal.Windows.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\System.Threading.Channels.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\System.Threading.Tasks.Extensions.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\System.ValueTuple.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\Triggerless.PlugIn.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\Triggerless.XAFLib.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\WindowsInput.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseBin}\TriggerBot.exe.config"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{userdesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\{#MyAppIcoName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

